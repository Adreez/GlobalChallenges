repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    //cmdAPI
    maven { url = "https://repo.codemc.org/repository/maven-public/" }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.20.4-R0.1-SNAPSHOT"
    implementation "net.kyori:adventure-platform-bukkit:4.3.1"

    implementation "net.kyori:adventure-text-minimessage:4.14.0"

    //cmdAPI
    compileOnly "dev.jorel:commandapi-bukkit-core:9.3.0"

    implementation project(':api')
    implementation project(':core')
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: rootProject.version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    //relocate 'sk.adr3ez.alib', 'sk.adr3ez.darkauth.alib'

    archiveFileName.set("GlobalChallenges-${rootProject.version}.jar")
}
